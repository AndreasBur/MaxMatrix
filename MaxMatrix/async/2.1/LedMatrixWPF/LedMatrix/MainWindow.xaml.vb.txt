Imports System.IO.Ports
Imports System.Windows.Threading


Class MainWindow

#Region "Definitionen"
    Private Class LedArrayTag
        Public Column As Integer
        Public Row As Integer
        Public LedOnState As Boolean
    End Class
#End Region

#Region "Function Prototypes"
    Delegate Sub SetTextCallback(ByVal Text As String)
#End Region

#Region "Global Variables"
    Dim PortOpenState As Boolean 'port state opened/closed
    Dim Serial As SerialPort = New SerialPort
    Dim LedArray(7, 7) As Image
    Private this As Window
#End Region

#Region "Subs"
    Sub SerialComs()
        SerialPortComboBox.Items.Clear()
        'Zeigt alle verfügbaren COM-Ports in ComboBox
        For Each COMPort As String In My.Computer.Ports.SerialPortNames
            SerialPortComboBox.Items.Add(COMPort)
        Next
    End Sub

    Private Sub Serial_DataReceivedHandler(sender As System.Object, e As System.IO.Ports.SerialDataReceivedEventArgs)
        'Liest Daten auf SerialPort
        ReceivedText(Serial.ReadExisting())
    End Sub

    Private Sub ReceivedText(ByVal Text As String)
        'Leitet empfangene Daten mittels des Delegaten von SerialPort in RTB1
        If LogTextBox.Dispatcher.CheckAccess() Then
            LogTextBox.AppendText(Text)
        Else
            LogTextBox.Dispatcher.Invoke(DispatcherPriority.Normal, New SetTextCallback(AddressOf ReceivedText), Text)
        End If
    End Sub

    Private Sub EnableControls(ByVal EnableState As Boolean)
        LedMatrixText.IsEnabled = EnableState
        IntensitySlider.IsEnabled = EnableState
        SpeedSlider.IsEnabled = EnableState
        IntensityValue.IsEnabled = EnableState
        SpeedValue.IsEnabled = EnableState
        DeleteButton.IsEnabled = EnableState
        SendButton.IsEnabled = EnableState
        ShiftCheckBox.IsEnabled = EnableState

        For n = 0 To 7 Step 1
            For i = 0 To 7 Step 1
                LedArray(n, i).IsEnabled = EnableState
            Next
        Next
    End Sub
#End Region

#Region "control elements"

    Private Sub Window_Loaded(sender As Object, e As RoutedEventArgs)
        PortOpenState = False
        Serial.Encoding = System.Text.Encoding.Default
        SerialComs()
        AddHandler Serial.DataReceived, AddressOf Serial_DataReceivedHandler

        For n = 0 To 7 Step 1
            For i = 0 To 7 Step 1
                LedArray(n, i) = New Image
                LedArray(n, i).Source = New BitmapImage(New Uri("pack://siteoforigin:,,,/Resources/led_green_off_big.png", UriKind.RelativeOrAbsolute))
                LedArray(n, i).IsEnabled = False
                LedArray(n, i).Width = 25
                LedArray(n, i).Height = 25
                LedArray(n, i).VerticalAlignment = VerticalAlignment.Top
                LedArray(n, i).HorizontalAlignment = HorizontalAlignment.Left
                LedArray(n, i).Margin = New Thickness(90 + i * 30, 25 + n * 30, 0, 0)
                LedMatrixStateGrid.Children.Add(LedArray(n, i))
                LedArray(n, i).AddHandler(Image.MouseLeftButtonUpEvent, New RoutedEventHandler(AddressOf LedArray_Click))
                LedArray(n, i).Tag = New LedArrayTag
                LedArray(n, i).Tag.Column = i
                LedArray(n, i).Tag.Row = n
                LedArray(n, i).Tag.LedOnState = False
            Next
        Next
    End Sub

    Private Sub Window_Closed(sender As Object, e As EventArgs)
        Serial.Close()
    End Sub

    Private Sub LedArray_Click(sender As Object, e As EventArgs)
        Dim Led = DirectCast(sender, Image)

        If Led.Tag.LedOnState = False Then
            Led.Tag.LedOnState = True
            Led.Source = New BitmapImage(New Uri("pack://siteoforigin:,,,/Resources/led_green_on_big.png", UriKind.RelativeOrAbsolute))
        ElseIf Led.Tag.LedOnState = True Then
            Led.Tag.LedOnState = False
            Led.Source = New BitmapImage(New Uri("pack://siteoforigin:,,,/Resources/led_green_off_big.png", UriKind.RelativeOrAbsolute))
        End If

        If PortOpenState = True Then
            If Led.Tag.LedOnState Then
                Serial.WriteLine("setDot=" + Led.Tag.Column.ToString + "," + Led.Tag.Row.ToString + "," + "1")
            Else
                Serial.WriteLine("setDot=" + Led.Tag.Column.ToString + "," + Led.Tag.Row.ToString + "," + "0")
            End If
        End If
    End Sub

    Private Sub SendButton_Click(sender As Object, e As RoutedEventArgs) Handles SendButton.Click
        If ShiftCheckBox.IsChecked Then
            Serial.WriteLine("textShift=" + LedMatrixText.Text)
        Else
            Serial.WriteLine("text=" + LedMatrixText.Text)
        End If
    End Sub

    Private Sub OpenButton_Click(sender As Object, e As RoutedEventArgs) Handles OpenButton.Click
        If PortOpenState = False Then
            If SerialPortComboBox.SelectedIndex <> -1 Then
                Serial.PortName = SerialPortComboBox.SelectedItem
                Serial.Open()
                SerialPortStatusImage.Source = New BitmapImage(New Uri("pack://siteoforigin:,,,/Resources/led_green_on_big.png", UriKind.RelativeOrAbsolute))
                OpenButton.Content = "Schließen"
                EnableControls(True)
                PortOpenState = True
            End If
        Else
            Serial.Close()
            SerialPortStatusImage.Source = New BitmapImage(New Uri("pack://siteoforigin:,,,/Resources/led_green_off_big.png", UriKind.RelativeOrAbsolute))
            OpenButton.Content = "Öffnen"
            EnableControls(False)
            PortOpenState = False
        End If
    End Sub

    Private Sub DeleteButton_Click(sender As Object, e As RoutedEventArgs) Handles DeleteButton.Click
        LedMatrixText.Clear()
        LogTextBox.Clear()
    End Sub

    Private Sub IntensitySlider_ValueChanged(sender As Object, e As RoutedPropertyChangedEventArgs(Of Double)) Handles IntensitySlider.ValueChanged
        If PortOpenState = True Then
            Serial.WriteLine("intensity=" + IntensitySlider.Value.ToString)
        End If
    End Sub

    Private Sub SpeedSlider_ValueChanged(sender As Object, e As RoutedPropertyChangedEventArgs(Of Double)) Handles SpeedSlider.ValueChanged
        If PortOpenState = True Then
            Serial.WriteLine("speed=" + SpeedSlider.Value.ToString)
        End If
    End Sub

    Private Sub CloseMenuItem_Click(sender As Object, e As RoutedEventArgs)
        System.Windows.Application.Current.MainWindow.Close()
    End Sub

    Private Sub AboutMenuItem_Click(sender As Object, e As RoutedEventArgs)
        Dim About As AboutWindow = New AboutWindow
        About.Owner = Application.Current.MainWindow
        About.WindowStartupLocation = WindowStartupLocation.CenterOwner
        About.Show()
    End Sub

    Private Sub SerialPortComboBox_DropDownOpened(sender As Object, e As EventArgs) Handles SerialPortComboBox.DropDownOpened
        SerialComs()
    End Sub
#End Region

End Class
